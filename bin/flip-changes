#!/usr/bin/env perl
use strict;
use warnings;

use Jifty;
BEGIN { Jifty->new }

$| = 1;

my $changelogs = App::Changeloggr::Model::ChangelogCollection->new(
    current_user => App::Changeloggr::CurrentUser->superuser,
);
$changelogs->unlimit;

while (my $changelog = <$changelogs>) {
    my $changes = $changelog->changes;
    $changes->order_by(
        column => 'id',
        order  => 'desc',
    );

    while (my $change = <$changes>) {
        my %values = $change->as_hash;
        delete $values{id};

        my $new_change = App::Changeloggr::Model::Change->new(
            current_user => App::Changeloggr::CurrentUser->superuser,
        );
        my ($ok, $msg) = $new_change->create(%values);
        if ($ok) { print '.' }
        else { print "\nUnable to create change: $msg. BAILING!\n"; exit }

        for my $collection ($change->votes, $change->rewordings, $change->importances) {
            while (my $record = <$collection>) {
                $record->__set(
                    column => 'change_id',
                    value  => $new_change->id,
                );
            }
        }

        ($ok, $msg) = $change->delete;
        if ($ok) { print '.' }
        else { print "\nUnable to delete change " . $change->id . ": $msg\n" }
    }
}

